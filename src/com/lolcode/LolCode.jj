/**
 * LOLCODE grammar
 * A grammar for Adam Lindsay's LOLCODE programming language
 * http://lolcode.com
 *
 */
PARSER_BEGIN(LolCode)

package com.lolcode;

/**
 * A parser for Adam Lindsay's <a href="http://lolcode.com">LOLCODE</a> programming language
 *
 *
 * @author brianegge
 */
public final class LolCode {
}

PARSER_END(LolCode)

/*********************
 * Lexer specification
 *********************/

TOKEN : 
{
  < HAI : "HAI" >
| < CAN : "CAN" >
| < HAS : "HAS" >
| < A : "A" >
| < I : "I" >
| < N : "N" >
| < IM : "IM" >
| < IN : "IN" >
| < IZ : "IZ" >
| < YR : "YR" >
| < UP : "UP" >
| < KTHX : "KTHX" >
| < KTHXBYE : "KTHXBYE" >
| < LOOP : "LOOP" >
| < THAN : "THAN" >
| < STDIO : "STDIO" >
| < OUTTA : "OUTTA" >
| < BIGGER : "BIGGER" >
| < GIMMEH : "GIMMEH" >
| < PLZ : "PLZ" >
| < AWSUM_THX : "AWSUM THX" >
| < O_NOES : "O NOES" >
| < O_RLY : "O RLY" >
| < YA_RLY : "YA RLY" >
| < NO_WAI : "NO WAI" >
| < OPEN : "OPEN" >
| < QUESTION : "?" >
| < VISIBLE : "VISIBLE" >
| < INVISIBLE : "INVISIBLE" >
| < BANG : "!!" >
| < QUOTED_STRING : "\"" (~["\""])+ "\"" >
| < INTEGER : ["1"-"9"] (["0"-"9"])* >
| < IDENT : (["A"-"Z"])+ >
| < EOL : "\n" | "\r" | "\r\n" >
}

SKIP :
{
 " " | "\t"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "BTW" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}


/****************
 * Grammer Specification
 ****************/

/**
 * Execute the parsed Mars script
 */
LolTree start() :
{
  LolTree tree = new LolTree(null);
  Statement s;
}
{
  <HAI> <EOL>
  (
    s = statement()
    { tree.add(s); }
   )* 
  <KTHXBYE>
  {
    return tree;
  }
}

Statement statement() :
{
  Statement s = null;
}
{
  <CAN> <HAS> <STDIO> <QUESTION> <EOL> { return null; }
| s = write() <EOL> { return s; }
| loop() { return null; }
| defineVariable() { return null; }
| <GIMMEH> <IDENT> <EOL> { return null; }
| function() { return null; }
| trycatch() { return null; }
}

void loop() :
{}
{
  <IM> <IN> <YR> <LOOP> <EOL>
  ( LOOKAHEAD(2) statement() )+
  <IM> <OUTTA> <YR> <LOOP> <EOL>
}

void defineVariable() :
{}
{
  <I> <HAS> <A> <IDENT> <EOL>
}

void function() :
{}
{
  <UP> <IDENT> <BANG> <INTEGER> <EOL>
|  LOOKAHEAD(2147483647) <IZ> expr() <QUESTION> <KTHXBYE> <EOL>
|  <IZ> expr() <O_RLY> <QUESTION> <EOL> then() ( Else() )? <KTHX> <EOL>
| <OPEN> <IDENT> <QUOTED_STRING>
}

void expr() :
{}
{
  <IDENT> <BIGGER> <THAN> <INTEGER>
}

void then() :
{}
{
  <YA_RLY> <EOL>
  ( statement() )*
}

void Else() :
{}
{
  <NO_WAI> <EOL>
  ( statement() )*
}

void trycatch() :
{}
{
  <PLZ> statement() <QUESTION> <EOL>
  <AWSUM_THX> <EOL>
    statement()
  <O_NOES> <EOL>
    statement()
}

StatementWrite write() :
{
  StatementWrite s = new StatementWrite();
  Object o;
}
{
  ( <VISIBLE>
   { s.setVisible(true); }
  | <INVISIBLE>
  )
   ( o = string() | o = <IDENT> )
    { s.add(o); }
    ( <N>
       ( o = string() | o = <IDENT> )
       { s.add(o); }
    )*
  {
    return s;
  }
}

int integer() :
{
 Token t;
}
{
  t=<INTEGER>
  {
    return Integer.parseInt(t.image);
  }
}

String string() :
{
 Token t;
}
{
 t = <QUOTED_STRING>
 {
   return t.image.substring(1, t.image.length() - 2);
 }
}
 